// Universit√© Paris-Saclay
// Lab : LISN ~ UMR9015 ~ BIOINFO team 

initialize() {
	initializeTreeSeq();
	defineConstant("pop_size", popsize);	// K = Pop size (Ne)
	defineConstant("generationsmitoses", generationmitoses); // 1/alpha (GR)
	defineConstant("recombination_rate", recombinationrate); // recombination rate (rho)
	defineConstant("expansion_ratio", expansionratio); // size population after expansion = size population before expansion * expansion_ratio
	defineConstant("rep", replicatenumber);

	
	initializeMutationRate(0, 1999999); // Overlay for neutral part of the genome so mu = 0 in the simulations
	// no sweep so no mutation rate here. only in tree_sumstats.py
	
	initializeMutationType("m1", 0.5, "f", 0.0);
	m1.mutationStackPolicy = "l";
	initializeGenomicElementType("g1", m1, 1.0);
	
	initializeRecombinationRate(c(recombination_rate*generationsmitoses, 0.5, recombination_rate*generationsmitoses), c(999999,1000000,1999999)); // Two independant chromosomes 

	initializeGenomicElement(g1, 0, 1999999);
	
	// Variable to store the generation for the next sexual event to happen
	// FANNY changed to a poisson distribution
	//defineGlobal("gen_recomb", asInteger(rnorm(1,generationsmitoses,asInteger(generationsmitoses/10))));
	defineGlobal("gen_recomb", asInteger(rpois(1, generationsmitoses)));
}

1 early(){ sim.addSubpop("p1", pop_size);}

early() {

	// If facultative sex (alpha < 1): do cloning then sex at the "gen_recomb" generation (also define the next gen_recom)
	// If not (alpha = 1) : do sex by default

	if(generationsmitoses!=1){ // <=> if alpha < 1
		if (sim.cycle != gen_recomb){
			if( (8000 <= sim.cycle) & (sim.cycle <= 10000) ) { // if we are in the expansion phase the cloning rate is the expansion_ratio
				//new_pop_size = asInteger(round(expansion_ratio^(sim.cycle - 7999) * pop_size));
				new_pop_size = asInteger(round(  (expansion_ratio^(1/2000))^(sim.cycle - 7999) * pop_size));
				p1.setSubpopulationSize(new_pop_size);	
			}
			
			p1.setCloningRate(1.0); // if not the cloning rate is 1
			
		}
		else {  
			if( (8000 <= sim.cycle) & (sim.cycle <= 10000) ) { // if we are in the expansion phase the population size is going to change
				new_pop_size = asInteger(round((expansion_ratio^(1/2000))^(sim.cycle - 7999) * pop_size));
				p1.setSubpopulationSize(new_pop_size);
			}
			p1.setCloningRate(0.0);
			defineGlobal("gen_recomb", gen_recomb + asInteger(rnorm(1,generationsmitoses,asInteger(generationsmitoses/10))));
		}
	}


	// added
	else {  // if generationsmitoses = 1 (sex at every generation)
			
		if( (8000 <= sim.cycle) & (sim.cycle <= 10000) ) { // if we are in the expansion phase the population size is going to change
             new_pop_size = asInteger(round((expansion_ratio^(1/2000))^(sim.cycle - 7999) * pop_size));
             p1.setSubpopulationSize(new_pop_size);
      	}
	}
	
}
   

10000 late() { // end at the 4000 generation 
	

	sim.treeSeqOutput(paste("results/neutral/expansion/ts_",rep,"_",generationsmitoses,".trees",sep=""));
	sim.outputFixedMutations();
	sim.simulationFinished();

}
